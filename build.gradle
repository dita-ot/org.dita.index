/*
 * This file is part of the DITA Open Toolkit project.
 *
 * Copyright 2015 Jarno Elovirta
 *
 * See the accompanying LICENSE file for applicable license.
 */
apply plugin: 'java'
apply plugin: 'maven'

group = 'org.dita-ot.index'
version = '1.0.0'
description = """DITA Open Toolkit indexing plug-in."""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}
dependencies {
    compile group: 'org.dita-ot', name: 'dost', version: '[2.1,)'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.xmlunit', name: 'xmlunit-core', version: '2.6.3'
    testCompile group: 'org.xmlunit', name: 'xmlunit-matchers', version: '2.6.3'
}

sourceSets {
    main {
        resources {
            exclude 'build.xml'
//            exclude 'catalog_template.xml'
            exclude 'messages.xml'
            exclude 'plugin.xml'
//            exclude 'xsl/*'
        }
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = "UTF-8"

jar.setArchiveName "${project.name}.jar"

task copyInstall(type: Copy) {
    from(configurations.runtime.allArtifacts.files)
    destinationDir = file("lib")
}

task dist(type: Zip) {
    from(configurations.runtime.allArtifacts.files) {
        into "lib"
    }
    from("src/main/resources") {
        include "plugin.xml"
        expand(
                version: project.version,
                jar: jar.archiveName
        )
    }
    from("src/main/resources") {
        include "build.xml"
//        include "catalog_template.xml"
        include "messages.xml"
    }
    from(".") {
        include "LICENSE"
    }
    archiveName "${project.name}-${project.version}.zip"
}
