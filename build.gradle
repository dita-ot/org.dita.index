/*
 * This file is part of the DITA Open Toolkit project.
 *
 * Copyright 2019 Jarno Elovirta
 *
 * See the accompanying LICENSE file for applicable license.
 */
plugins {
    id "com.diffplug.gradle.spotless" version "3.26.0"
}

apply plugin: 'java'
apply plugin: 'maven'

group = 'org.dita-ot'
version = '1.0.0'
description = """DITA Open Toolkit indexing plug-in."""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}
dependencies {
    compile group: 'org.dita-ot', name: 'dost', version: '[2.1,)'
    runtime group: 'com.ibm.icu', name: 'icu4j-localespi', version: '65.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.xmlunit', name: 'xmlunit-core', version: '2.6.3'
    testCompile group: 'org.xmlunit', name: 'xmlunit-matchers', version: '2.6.3'
}

sourceSets {
    main {
        resources {
            exclude 'build.xml'
            exclude 'messages.xml'
            exclude 'plugin.xml'
        }
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = "UTF-8"

jar.setArchiveFileName "${project.name}.jar"

task copyInstall(type: Copy) {
    from(configurations.runtime.allArtifacts.files)
    destinationDir = file("lib")
}

task dist(type: Zip) {
    from(sourceSets.main.runtimeClasspath) {
        include "icu4j-localespi-*.jar"
        into "lib"
    }
    from(configurations.runtime.allArtifacts.files) {
        into "lib"
    }
    from("src/main/resources") {
        include "plugin.xml"
        expand(
                version: project.version,
                jar: jar.archiveName
        )
    }
    from("src/main/resources") {
        include "build.xml"
        include "index/*"
        include "messages.xml"
    }
    from(".") {
        include "LICENSE"
    }
    archiveName "org.dita.${project.name}-${project.version}.zip"
}

spotless {
    java {
        googleJavaFormat()
    }
}

task fmt(dependsOn: spotlessApply)
